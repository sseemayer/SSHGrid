#!/usr/bin/perl

=pod

=head1 NAME

sshgrid-kill - Send signals to sshgrid-worker processes

=head1 SYNOPSIS

	$ sshgrid-status -h hosts-file -s "2011-01-01-12-00-00" -S "SIGKILL"

Options:

	-h	--hosts-file		Filename pointing to list of hostnames to use for processing.
	-s	--subdirectory		Name of the subdirectory in the base directory for which to query status
	-S	--Signal		Name of the signal to send. Default: SIGKILL

	-b	--base-directory	Base directory on hosts for SSHGrid management. Default: ~/.sshgrid
	-w	--worker-file		Filename for the worker script running on the cluster nodes. Default: sshgrid-worker

	-?	--help			Display short help message
		--man			Display manpage


=head1 DESCRIPTION

sshgrid-kill will SSH into the first host in the provied hosts file, get the list of worker processes from the provided subdirectory and will use the hostname and pid information 
to send a signal to all worker processes.

=head2 Supported Signals

The following signals can be specified by the -S option: 

SIGHUP, SIGINT, SIGQUIT, SIGABRT, SIGKILL, SIGTERM, SIGSTOP, SIGCONT

=head1 EXAMPLES

All examples assume that the subdirectory for computation is stored in $SUBDIR. (e.g. SUBDIR=2011-01-01-12-00-00)

Cancelling computation in all workers (by sending a SIGKILL):

	sshgrid-kill -h hosts.txt -s $SUBDIR -s SIGKILL

Suspending computation in all workers and then waking up:

	sshgrid-kill -h hosts.txt -s $SUBDIR -s SIGSTOP
	sshgrid-kill -h hosts.txt -s $SUBDIR -s SIGCONT

=head1 BUGS

Highly likely, and SSHGrid is still feature-incomplete, so check the SSHGrid issue tracker to report problems:

https://github.com/sseemayer/SSHGrid/issues

=head1 AUTHOR

Stefan Seemayer <seemayer@in.tum.de>

=cut

use strict;
use warnings;
use diagnostics;

use Getopt::Long qw(:config no_ignore_case );
use Pod::Usage;
use File::Basename;

use SSHGrid;

my ($help, $man, $hosts_file, $subdirectory, $host_count);
my $worker_name = "sshgrid-worker";
my $base_directory = "~/.sshgrid";
my $signal = "SIGTERM";

my %valid_signals = ( 
	SIGHUP => 1,
	SIGINT => 2,
	SIGQUIT	=> 3,
	SIGABRT	=> 6,	
	SIGKILL	=> 9, 	
	SIGTERM	=> 15, 	
	SIGSTOP => 17,
	SIGCONT => 19,
);

GetOptions(	
		"h|hosts-file=s"	=> \$hosts_file,
		"s|subdirectory=s"	=> \$subdirectory,
		"S|Signal=s"		=> \$signal,

		"b|base-directory=s"	=> \$base_directory,
		"w|worker-file=s"	=> \$worker_name,

		"help|?"		=> \$help,
		"man"			=> \$man,
) or pod2usage(2);

pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;

unless($hosts_file) {
	print STDERR "Must provide a hosts file!\n\n";
	pod2usage(2);
}

unless($subdirectory) {
	print STDERR "Must provide a subdirectory!\n\n";
	pod2usage(2);
}

unless(defined $valid_signals{$signal}) {
	print STDERR "Not a valid signal: $signal\nPossible choices:\n" . join(", ", sort keys %valid_signals) . "\n\n";
	exit(1);
}

my $sshg = SSHGrid->new(
	HostsSource => $hosts_file,
	RemoteRoot => $base_directory,
);


# launch worker scripts on all hosts
my $workdir = "$base_directory/$subdirectory";
for my $hostset (@{$sshg->get_host_sets()}) {
	my $name = $hostset->get_name;
	$sshg->remote_command($hostset->get_set_master, "( cd $workdir; chmod +x $worker_name; ./$worker_name kill -$signal 2>&1)");
	
}

