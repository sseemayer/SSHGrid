#!/usr/bin/perl

=pod

=head1 NAME

sshgrid-do - Run a list of commands on an SSH grid 

=head1 SYNOPSIS

	$ sshgrid-do -h hosts -c commands.sh

Options:

	-h	--hosts-file		Filename pointing to list of hostnames to use for processing.
	-c	--commands-file		Filename pointing to list of commands to be executed

	-s	--subdirectory		Give a subdirectory name. If unspecified, will create a timestamped subdirectory.

	-b	--base-directory	Base directory on hosts for SSHGrid management. Default: ~/.sshgrid
	-w	--worker-file		Filename for the worker script to run on the cluster nodes

	-?	--help			Display short help message
		--man			Display manpage


=head1 DESCRIPTION

sshgrid-do will take a hosts and commands file, copy the commands file and a worker script to the first host 
in the hosts list and then run the worker script in parallel on all hosts. 

=head2 Hosts files

Host files are lists of hostnames (one per file) that can be SSHed to from the machine running sshgrid-do,
grouped by the common filesystems that they share. You may optionally specify a username like in a call to SSH:

	[ Compute Pool 1 ]
		jdoe@node1.cippool.host.org
		jdoe@node2.cippool.host.org
		jdoe@node3.cippool.host.org
		jdoe@node4.cippool.host.org

	[ Compute Pool 2 ]
		johndoe@node1.anotherhost.com
		johndoe@node2.anotherhost.com
		johndoe@node3.anotherhost.com

=head2 Command files

Lines in command files can contain any command that could be run in a bash command line, but processing of lines is
not deterministic - The worker scripts running on the cluster nodes will remove the topmost line of the script and
execute it, taking the next line as soon as they are finished, and so on, until the command file is empty.

Comments lines are allowed and are denoted by a # character as the _first_ character in the line.

=head1 EXAMPLES

Check the share/SSHGrid/examples/ directory that came in your SSHGrid distribution for sample command files and other goodies.

=head1 BUGS

Highly likely, and SSHGrid is still feature-incomplete, so check the SSHGrid issue tracker to report problems:

https://github.com/sseemayer/SSHGrid/issues

=head1 AUTHOR

Stefan Seemayer <seemayer@in.tum.de>

=cut

use strict;
use warnings;
use diagnostics;

use Getopt::Long;
use Pod::Usage;
use File::Basename;

use SSHGrid;

my ($help, $man, $hosts_file, $commands_file, $subdirectory);

my $base_directory = "~/.sshgrid";
my $worker_file = (fileparse(__FILE__))[1] . "../share/SSHGrid/node-bin/sshgrid-worker";

GetOptions(	
		"h|hosts-file=s"	=> \$hosts_file,
		"c|commands-file=s"	=> \$commands_file,

		"s|subdirectory=s"	=> \$subdirectory,

		"b|base-directory=s"	=> \$base_directory,
		"w|worker-file=s"	=> \$worker_file,

		"help|?"		=> \$help,
		"man"			=> \$man,
) or pod2usage(2);

pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;

unless($hosts_file) {
	print STDERR "Must provide a hosts file!\n\n";
	pod2usage(2);
}

unless($commands_file) {
	print STDERR "Must provide a commands file!\n\n";
	pod2usage(2);
}

unless(-f $worker_file) {
	print STDERR "Cannot locate worker at $worker_file!\n\n";
	pod2usage(2);
}

my $sshg = SSHGrid->new(
	HostsSource => $hosts_file,
	RemoteRoot => $base_directory,
);

unless(scalar @{$sshg->get_host_sets}) {
	print STDERR "Did not find any host sets in $hosts_file!\n\n";
	pod2usage(2);
}


my $subdir = defined $subdirectory ? $subdirectory : $sshg->get_new_subdir();

print STDERR "Uploading SSHGrid base system...\n";
for my $hostset (@{$sshg->get_host_sets()}) {
	my $name = $hostset->get_name();
	my $set_master = $hostset->get_set_master();

	print STDERR "\n\t$set_master for '$name':\n";

	# install sshgrid system
	$sshg->upload(
		Host		=> $set_master,
		Filenames 	=> [$worker_file],
		Subdir		=> $subdir,
	);
}

my $master = $sshg->get_host_sets->[0]->get_hosts->[0];

print STDERR "Uploading commands file to master $master...\n";
system("scp $commands_file $master:$base_directory/sshgrid.commands.sh") or die("Upload failed: $!");

# launch worker scripts on all hosts
my $worker_name = (fileparse($worker_file))[0];
my $workdir = "$base_directory/$subdir";
my $hosts = $sshg->hosts;

print STDERR "\nLaunching worker processes...\n";
for my $hostset ($sshg->get_host_sets()) {

	for my $host (@{$hostset->get_hosts()}) {
		$sshg->remote_command($host, "( cd $workdir; chmod +x $worker_name; ./$worker_name run $master $hostno $host_count 2>&1 > host-$hostno.log)", 1);
	}
	
}

print STDERR "Finished launching SSHGrid workers.\n\n";
print STDERR "Your work subdir is:\t$subdir\n"; 
print STDERR "Find results on $hosts->[0] at:\t$workdir\n\n";
